import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    dependencies {
        classpath 'mysql:mysql-connector-java:8.0.13'
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.flywaydb.flyway" version "5.2.1"
    id "com.gorylenko.gradle-git-properties" version "2.2.2"
    id "java"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'mysql:mysql-connector-java'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

def devDb = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
    "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always",
    "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
    "SPRING_DATASOURCE_URL": devDb,
    "WELCOME_MESSAGE": "hello",
])

def testDb = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
    "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "always",
    "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
    "SPRING_DATASOURCE_URL": testDb,
    "WELCOME_MESSAGE": "Hello from test",
])

flyway {
    url = devDb
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDb
}

springBoot {
    buildInfo()
}